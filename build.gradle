buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.7'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply from: "$rootDir/gradle/integration-test.gradle"
    apply from: "$rootDir/gradle/reporting.gradle"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    compileJava {
        options.encoding = 'UTF-8'
    }

    compileTestJava {
        options.encoding = 'UTF-8'
    }

    compileIntegTestJava {
        options.encoding = 'UTF-8'
    }

    javadoc {
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenLocal()
        jcenter()
    }

    group 'fr.chaffotm.game.geo'
    version '0.1-SNAPSHOT'

    dependencies {
        implementation 'com.h2database:h2:1.4.199'

        testImplementation 'junit:junit:4.12'
        testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.3.2'
        testImplementation 'org.assertj:assertj-core:3.12.2'
        testImplementation 'org.mockito:mockito-core:2.25.1'

        integTestImplementation 'junit:junit:4.12'
        integTestRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.3.2'
        integTestImplementation 'org.assertj:assertj-core:3.12.2'
    }

    test {
        useJUnitPlatform()
        exclude '**/*Tests.class'
        beforeTest { descriptor ->
            logger.lifecycle("Running " + descriptor)
        }
    }
    test.outputs.upToDateWhen { false }

    integrationTest {
        useJUnitPlatform()
        exclude '**/*Tests.class'
        beforeTest { descriptor ->
            logger.lifecycle("Running " + descriptor)
        }
    }
    integrationTest.outputs.upToDateWhen { false }
}

wrapper {
    gradleVersion = '5.2.1'
}
