apply plugin: 'idea'

sourceSets {
    test {
        java.srcDir file('src/test/unit/java')
        resources.srcDir file('src/test/unit/resources')
    }

    integrationTest {
        java.srcDir file('src/test/integration/java')
        resources.srcDir file('src/test/integration/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    classpath = sourceSets.integrationTest.runtimeClasspath
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    mustRunAfter test
    outputs.upToDateWhen { false }
}

check.dependsOn integrationTest

idea {
    module {
        testSourceDirs += project.sourceSets.integrationTest.java.srcDirs
        testResourceDirs += project.sourceSets.integrationTest.resources.srcDirs
    }
}